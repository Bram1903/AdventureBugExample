plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.2.3'
}

group 'com.deathmotion.testplugin'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.lunarclient.dev'}
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    compileOnly 'com.lunarclient:apollo-api:1.1.2'
    compileOnly 'com.lunarclient:apollo-extra-adventure4:1.1.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.2'
}

tasks {
    shadowJar {
        minimize()
        archiveFileName.set("${project.name}-${project.version}.jar")

        relocate('net.kyori', 'com.deathmotion.testplugin.shaded.kyori')
    }

    build {
        dependsOn(shadowJar)
    }

    runServer {
        // The version of the server to run
        def version = "1.8.8"

        minecraftVersion(version)
        runDirectory.set(file("server/$version"))

        // 1.8.8 - 1.16.5 = Java 8
        // 1.17           = Java 16
        // 1.18 - 1.20.4  = Java 17
        javaLauncher.set(project.javaToolchains.launcherFor {
            languageVersion.set(JavaLanguageVersion.of(8))
        })

        downloadPlugins {
            url("https://github.com/LunarClient/Apollo/releases/download/v1.1.0/apollo-bukkit-1.1.0.jar")
        }

        jvmArgs = [
                "-Dcom.mojang.eula.agree=true"
        ]
    }
}